<html>
  <head>
    <title>INPUT, LINE INPUT Statements</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
  </head>
  <body>
    <pre class="help-content">  <i>◄</i><a href="T0002.html">Contents</a><i>►</i>  <i>◄</i><a href="T00B6.html">Index</a><i>►</i>  <i>◄</i><a href="?!B">Back</a><i>►</i>
──────────────────────────────────────────────────────────────────────────────
INPUT reads input from the keyboard or a file. LINE INPUT reads a line of
up to 255 characters from the keyboard or a file.

<b>INPUT [;] [&quot;prompt&quot;{; | ,}] variablelist</b>
<b>LINE INPUT [;] [&quot;prompt&quot;;] variable$</b>
<b>INPUT #filenumber%, variablelist</b>
<b>LINE INPUT #filenumber%, variable$</b>

    ■ <b>prompt</b>          An optional literal string that is displayed before
                      the user enters data. A semicolon after prompt appends
                      a question mark to the prompt string.
    ■ <b>variablelist</b>    One or more variables, separated by commas, in which
                      data entered from the keyboard or read from a file is
                      stored. Variable names can consist of up to 40
                      characters and must begin with a letter. Valid
                      characters are A-Z, 0-9, and period (.).
    ■ <b>variable$</b>       Holds a line of characters entered from the keyboard
                      or read from a file.
    ■ <b>filenumber%</b>     The number of an open file.

    ■ INPUT uses a comma as a separator between entries.
      LINE INPUT reads all characters up to a carriage return.
    ■ For keyboard input, a semicolon immediately after INPUT keeps the
      cursor on the same line after the user presses the Enter key.

Example:
    CLS
    OPEN &quot;LIST&quot; FOR OUTPUT AS #1
    DO
        INPUT &quot;   NAME:       &quot;, Name$  &apos;Read entries from the keyboard.
        INPUT &quot;   AGE:        &quot;, Age$
        WRITE #1, Name$, Age$
        INPUT &quot;Add another entry&quot;; R$
    LOOP WHILE UCASE$(R$) = &quot;Y&quot;
    CLOSE #1
    &apos;Echo the file back.
    OPEN &quot;LIST&quot; FOR INPUT AS #1
    CLS
    PRINT &quot;Entries in file:&quot;: PRINT
    DO WHILE NOT EOF(1)
        LINE INPUT #1, REC$  &apos;Read entries from the file.
        PRINT REC$           &apos;Print the entries on the screen.
    LOOP
    CLOSE #1
    KILL &quot;LIST&quot;

See Also    <i>◄</i><a href="T0035.html">INKEY$</a><i>►</i>    <i>◄</i><a href="T0024.html">INPUT$</a><i>►</i>    <i>◄</i><a href="T002C.html">OPEN Statement File Modes</a><i>►</i></pre>
  </body>
</html>
